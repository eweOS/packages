# Maintainer: Yukari Chiba <i@0x7f.cc>

pkgbase=font-nerd
pkgname=(otf-aurulent-nerd
         otf-codenewroman-nerd
         otf-comicshanns-nerd
         otf-commit-mono-nerd
         otf-droid-nerd
         otf-firamono-nerd
         otf-geist-mono-nerd
         otf-hasklig-nerd
         otf-hermit-nerd
         otf-monaspace-nerd
         otf-opendyslexic-nerd
         otf-overpass-nerd
         ttf-0xproto-nerd
         ttf-3270-nerd
         ttf-agave-nerd
         ttf-anonymouspro-nerd
         ttf-arimo-nerd
         ttf-bigblueterminal-nerd
         ttf-bitstream-vera-mono-nerd
         ttf-cascadia-code-nerd
         ttf-cascadia-mono-nerd
         ttf-cousine-nerd
         ttf-d2coding-nerd
         ttf-daddytime-mono-nerd
         ttf-dejavu-nerd
         ttf-envycoder-nerd
         ttf-fantasque-nerd
         ttf-firacode-nerd
         ttf-go-nerd
         ttf-gohu-nerd
         ttf-hack-nerd
         ttf-heavydata-nerd
         ttf-iawriter-nerd
         ttf-ibmplex-mono-nerd
         ttf-inconsolata-go-nerd
         ttf-inconsolata-lgc-nerd
         ttf-inconsolata-nerd
         ttf-intone-nerd
         ttf-iosevka-nerd
         ttf-iosevkaterm-nerd
         ttf-iosevkatermslab-nerd
         ttf-jetbrains-mono-nerd
         ttf-lekton-nerd
         ttf-liberation-mono-nerd
         ttf-lilex-nerd
         ttf-martian-mono-nerd
         ttf-meslo-nerd
         ttf-monofur-nerd
         ttf-monoid-nerd
         ttf-mononoki-nerd
         ttf-mplus-nerd
         ttf-noto-nerd
         ttf-profont-nerd
         ttf-proggyclean-nerd
         ttf-recursive-nerd
         ttf-roboto-mono-nerd
         ttf-sharetech-mono-nerd
         ttf-sourcecodepro-nerd
         ttf-space-mono-nerd
         ttf-terminus-nerd
         ttf-tinos-nerd
         ttf-ubuntu-mono-nerd
         ttf-ubuntu-nerd
         ttf-victor-mono-nerd
         ttf-zed-mono-nerd)
pkgdesc="Iconic font aggregator, collection, and patcher"
pkgver=3.2.1
pkgrel=1
arch=('any')
url='https://github.com/ryanoasis/nerd-fonts'
_urlbase="${url}/releases/download/v${pkgver}"
license=('MIT' 'OFL-1.1')
options=('!debug' '!strip')
provides=('ttf-font-nerd')
groups=('nerd-fonts')
source=("0xproto-${pkgver}.tar.xz::${_urlbase}/0xProto.tar.xz"
        "3270-${pkgver}.tar.xz::${_urlbase}/3270.tar.xz"
        "agave-${pkgver}.tar.xz::${_urlbase}/Agave.tar.xz"
        "anonymouspro-${pkgver}.tar.xz::${_urlbase}/AnonymousPro.tar.xz"
        "arimo-${pkgver}.tar.xz::${_urlbase}/Arimo.tar.xz"
        "aurulent-${pkgver}.tar.xz::${_urlbase}/AurulentSansMono.tar.xz"
        "bigblueterminal-${pkgver}.tar.xz::${_urlbase}/BigBlueTerminal.tar.xz"
        "bitstreamveramono-${pkgver}.tar.xz::${_urlbase}/BitstreamVeraSansMono.tar.xz"
        "cascadiacode-${pkgver}.tar.xz::${_urlbase}/CascadiaCode.tar.xz"
        "cascadiamono-${pkgver}.tar.xz::${_urlbase}/CascadiaMono.tar.xz"
        "codenewroman-${pkgver}.tar.xz::${_urlbase}/CodeNewRoman.tar.xz"
        "comicshanns-${pkgver}.tar.xz::${_urlbase}/ComicShannsMono.tar.xz"
        "commitmono-${pkgver}.tar.xz::${_urlbase}/CommitMono.tar.xz"
        "cousine-${pkgver}.tar.xz::${_urlbase}/Cousine.tar.xz"
        "daddytimemono-${pkgver}.tar.xz::${_urlbase}/DaddyTimeMono.tar.xz"
        "d2coding-${pkgver}.tar.xz::${_urlbase}/D2Coding.tar.xz"
        "dejavu-${pkgver}.tar.xz::${_urlbase}/DejavuSansMono.tar.xz"
        "droid-${pkgver}.tar.xz::${_urlbase}/DroidSansMono.tar.xz"
        "envycoder-${pkgver}.tar.xz::${_urlbase}/EnvyCodeR.tar.xz"
        "fantasque-${pkgver}.tar.xz::${_urlbase}/FantasqueSansMono.tar.xz"
        "fira-${pkgver}.tar.xz::${_urlbase}/FiraCode.tar.xz"
        "firamono-${pkgver}.tar.xz::${_urlbase}/FiraMono.tar.xz"
        "geistmono-${pkgver}.tar.xz::${_urlbase}/GeistMono.tar.xz"
        "go-${pkgver}.tar.xz::${_urlbase}/Go-Mono.tar.xz"
        "gohu-${pkgver}.tar.xz::${_urlbase}/Gohu.tar.xz"
        "hack-${pkgver}.tar.xz::${_urlbase}/Hack.tar.xz"
        "hasklig-${pkgver}.tar.xz::${_urlbase}/Hasklig.tar.xz"
        "heavydata-${pkgver}.tar.xz::${_urlbase}/HeavyData.tar.xz"
        "hermit-${pkgver}.tar.xz::${_urlbase}/Hermit.tar.xz"
        "iawriter-${pkgver}.tar.xz::${_urlbase}/iA-Writer.tar.xz"
        "ibmplexmono-${pkgver}.tar.xz::${_urlbase}/IBMPlexMono.tar.xz"
        "inconsolata-${pkgver}.tar.xz::${_urlbase}/Inconsolata.tar.xz"
        "inconsolatago-${pkgver}.tar.xz::${_urlbase}/InconsolataGo.tar.xz"
        "inconsolatalgc-${pkgver}.tar.xz::${_urlbase}/InconsolataLGC.tar.xz"
        "intone-${pkgver}.tar.xz::${_urlbase}/IntelOneMono.tar.xz"
        "iosevka-${pkgver}.tar.xz::${_urlbase}/Iosevka.tar.xz"
        "iosevkaterm-${pkgver}.tar.xz::${_urlbase}/IosevkaTerm.tar.xz"
        "iosevkatermslab-${pkgver}.tar.xz::${_urlbase}/IosevkaTermSlab.tar.xz"
        "jetbrainsmono-${pkgver}.tar.xz::${_urlbase}/JetBrainsMono.tar.xz"
        "lekton-${pkgver}.tar.xz::${_urlbase}/Lekton.tar.xz"
        "liberationmono-${pkgver}.tar.xz::${_urlbase}/LiberationMono.tar.xz"
        "lilex-${pkgver}.tar.xz::${_urlbase}/Lilex.tar.xz"
        "martianmono-${pkgver}.tar.xz::${_urlbase}/MartianMono.tar.xz"
        "meslo-${pkgver}.tar.xz::${_urlbase}/Meslo.tar.xz"
        "monaspace-${pkgver}.tar.xz::${_urlbase}/Monaspace.tar.xz"
        "monofur-${pkgver}.tar.xz::${_urlbase}/Monofur.tar.xz"
        "monoid-${pkgver}.tar.xz::${_urlbase}/Monoid.tar.xz"
        "mononoki-${pkgver}.tar.xz::${_urlbase}/Mononoki.tar.xz"
        "mplus-${pkgver}.tar.xz::${_urlbase}/MPlus.tar.xz"
        "noto-${pkgver}.tar.xz::${_urlbase}/Noto.tar.xz"
        "opendyslexic-${pkgver}.tar.xz::${_urlbase}/OpenDyslexic.tar.xz"
        "overpass-${pkgver}.tar.xz::${_urlbase}/Overpass.tar.xz"
        "profont-${pkgver}.tar.xz::${_urlbase}/ProFont.tar.xz"
        "proggyclean-${pkgver}.tar.xz::${_urlbase}/ProggyClean.tar.xz"
        "recursive-${pkgver}.tar.xz::${_urlbase}/Recursive.tar.xz"
        "robotomono-${pkgver}.tar.xz::${_urlbase}/RobotoMono.tar.xz"
        "sharetechmono-${pkgver}.tar.xz::${_urlbase}/ShareTechMono.tar.xz"
        "sourcecodepro-${pkgver}.tar.xz::${_urlbase}/SourceCodePro.tar.xz"
        "spacemono-${pkgver}.tar.xz::${_urlbase}/SpaceMono.tar.xz"
        "terminus-${pkgver}.tar.xz::${_urlbase}/Terminus.tar.xz"
        "tinos-${pkgver}.tar.xz::${_urlbase}/Tinos.tar.xz"
        "ubuntu-${pkgver}.tar.xz::${_urlbase}/Ubuntu.tar.xz"
        "ubuntumono-${pkgver}.tar.xz::${_urlbase}/UbuntuMono.tar.xz"
        "victormono-${pkgver}.tar.xz::${_urlbase}/VictorMono.tar.xz"
        "zedmono-${pkgver}.tar.xz::${_urlbase}/ZedMono.tar.xz")
sha256sums=('cf165059645c02212fbdb5a27858056772d12a762468a0971c5bed3de88d0427'
            '6168e416a4331e8d59d5436d9a3c8170f98ee044e2fe662cbf1d7bea612f927d'
            'c307d029f638a411834972569317a04b9d75e968a53b12e41c787db064a6bef7'
            '1e4b7f7c8528c9d43760eacdb5d423ad272f028cad5958e594a30486dce409d3'
            '20f3bc015e46e4fe9f57cfe495eaa167b796e047df4f98676fe16649aba4ffb8'
            'd71be5a39c772a58d2592e490784298753573ee47836633ade381f1eeee39c72'
            '432c4a9080038554a94a46f435cb1aca3d58e23deb445e5c39d2c740a9edac84'
            '5328418c755f076363e46960f311c734c0939167ab02eb718a8fb7bcc1bbd2e8'
            '5af79aaebec3aa474c801e48aa2cb901cf1e9c03cbb3b6c189fed439e867e6e9'
            '9672540e8707d221fe48fa5da49aec5a605b2d38a85c9fed959eb76990b33367'
            '45d29f5be8add78ae271550d542387f4c98bbbe62e395a3646bc67f66f9d3c5c'
            'dc0835a985f19491526fb5773ad7d9646bbdb962cda5076615b686427fb565dc'
            '173f5a8fe5bb0a4e7db61ac9a77e97c7b5071b9b2a9f8dbaf8432311518a5f4a'
            'b8f14c715bdcce4afafd9c4e57acba5b9d5c1c3357f054a505716187ae1e98b1'
            '6e7dd3ebdb82ee4a5b358801c082553692431e246c996dcbb3a737a28c78e4db'
            'c01665970a43c13b27ea1fb9c85055d9c2369b3f9f2d1b37c08ffeaad7830cb0'
            'b94bde4d2e9ceb1f2c19b2846c1a9892797e4e15e9594303eb7088534244a18b'
            'd3910065466618646baf9a621d385f8122c310e29d449462e5bd25bd300e0dd7'
            'd1ca4f8bae74e089775a9ba6b2015ee79e03ec0970e76499f56131499524efa4'
            '2d6db874195ac7ec4d9b396a9612e1e342b060d8aef0304b0b99c49c3b9a351d'
            'def4b29f7aa0620a7fa12edb28197a2697680b5e21636fa3dcc602e08ae13bc4'
            'c0224b12e3da90550bb1c42a9149346326ebbc9f41cf83a5e6c8944e57772bc5'
            '0f247f19afece755de1a1b7b2eae38d48023ce4436323ab51f571d38314c6c73'
            '44a4ce529f51dc5f067a10b851acd06a1dc7ea7d6af5ba8a420e057232c8d948'
            '7ab613fac9d5931bd32d8e5a438ae0cb17536317e171f28ee5ddb8a171941041'
            '89b9023f3f6d5e4b8040022ff29b36faee440cf49015fda163215c922569b5f3'
            '4fb0ff16a5e587e65f59e55d08283729de700f0c0bf682ec1bef18ab51264ada'
            'f6b8a6cfb0d06bd25e7653c2ac18a562f53f6143eb131195a5fca4dde30d6aa8'
            '23552caf1b6075858756de2e8f3a3fbb9799b478882f3f7afeefa8ca7f4601ae'
            '85b0b39b30a9ddd89462b0a7a732b461de04f667dc01a5e987bf0c2accbb576f'
            '97e2774a01a151fc2f711f018831d24777472daf0f2822230ce20a7ac227a041'
            '2c9f5a1117e1b5a640287081680837e7b870c94868627766483011ff0b80c3f7'
            '69e0a42a6e8072b2a084f09a5056e857e9b620afd57d7d07c8d1da2cf4809b41'
            '7a96f8cc30e57a426ffce3b844deb10d6325181b28df9b76735308f2d787dc2d'
            'a016b61e49c395c5b1e5bd2c919ae668724a33f2ef14da3becf932da401a080d'
            '1be123fa355a9acee79d19891c02499792f6a4e12ffa8973c6b1cb738465f337'
            'b5d5e7b26c76f1cfe97a697716fdd682b18faf9f73883915c2b0c2d9fcc48cf5'
            '3132ff085ce404a3c065054638f3dc68de27d07a5cc5379b90b2a248877971eb'
            '6cf8822bc1ca18e34b06578c7499f380c019e6ffc883eed26df5f498dfcc4006'
            '2282cb16651a377d5b084486a268189fdafd9067775f1f24ffaf5a36a2459501'
            '9d0ad55cbd83967ca93e940f4951d41b23a44c892d9a4788af460d71f8bb81a4'
            'a2720d1fbf7ebb51b6a0ab191de41d96c7594d61f02c582db2249ea30c1ce263'
            '62484ea8f66ae3b75610b89c5d3258f8610844fec6d8402239c27a5042dccc7d'
            '1abdaf5542b75a51d75de6373148a8e401b28eda88e6c90f243fb6114fd659ea'
            'e5d2e8180d5df1fe473b598edac5f67ce38b1d906248936eb386dd332a338346'
            'eb035bfb09043db2278dd6f95ca956a7cc42a3e6cc30910295c51555b2e8ff81'
            'c7a5ff556ff4aad5381e8a57e0d17d79c591e1267f0a9c271b09306ac4581aba'
            'eb3697dd13c5e50951023c0768538a2f70e1e4f22d94800a45779a6d1b49f7a3'
            '5c60700788b093e0bfb30b2f59b52397f3fdc2cdf2b6fb9307a58b61e7ddfe74'
            '4be621219ff5508c4176487b4fdc8cd0f6d8d8fceeb935f83df11a49dca3096f'
            '96d43a8c53173b992537a1d34ae984f65a8999b7c8991a02c17d4b77497581fa'
            'fcb796892b105c98ad05a29f184f09a02038970ff6a95f8cda082cd1b1b08cde'
            'fe3bd035d43b943de17e6e0da1dfb0bd1dcc1b319e10c5e5925ab70436d48385'
            '1a58c57b63f380441ea07d44780680844afe12dd299651b87db59f9f311702d1'
            'e5f7db6999bf6dd6501d8933575caab49827486ce774c6c35a6e411da8c6cfba'
            '3ccfd4d8d625c708e593e46ea0404536984f69f11c7a08b5a59eb9bbc9dfde3c'
            'dbcfa99f4d41f05e4b59eb2197873515c035eff1a68800bf0f43db471ef7ba57'
            '56a43a2729035f7fec17b68823a7db9ea06fcf9ec8d0b5e70d73ab79488a1b21'
            'b6b9d7c604cc55d2c6859ad098371dfbfe540eb2d5aab4faad21f81149659338'
            '658cc9506acf89b5f5dfc6b55eb9899b782dcf8d89f3d11c9abb2c4d52617f12'
            '21b43c2314614733f5e9b4b4f3117edc3986d2b0f2a33409b111cc38360e8391'
            '6a254d48c837974e7c43fb92124e8e057c1d4b31c9ebe635f5c562233a24f3ad'
            '2b678521c5c8a963ba2c8f7846e21df4323838c64d70d59cb1ff38124d7f7396'
            'aa1b09dca861a306cd24c2c47fded88b63ae833ff0e815dad3642465583e300f'
            '2df9c9e54e5a2bc19c2ffd1df7ee283e02ba2e9a5fda1067b4884375ae4b0b7a')
noextract=("${source[@]%%::*}")

prepare() {
  # archives don't include a font-specific directory, so we have to make them
  for _font in 3270 agave anonymouspro arimo aurulent bigblueterminal \
               bitstreamveramono cascadiacode codenewroman cousine \
               daddytimemono dejavu droid fantasque fira firamono go hack \
               hasklig heavydata hermit iawriter ibmplexmono inconsolata \
               inconsolatago inconsolatalgc iosevka jetbrainsmono lekton \
               liberationmono lilex meslo monofur monoid mononoki mplus noto \
               opendyslexic overpass profont proggyclean robotomono \
               sharetechmono sourcecodepro spacemono terminus tinos ubuntu \
               ubuntumono victormono iosevkaterm comicshanns intone 0xproto \
               cascadiamono commitmono d2coding envycoder geistmono martianmono \
               monaspace gohu iosevkatermslab recursive zedmono; do
  rm -rf ${_font}
  mkdir ${_font}
  bsdtar xf ${_font}-${pkgver}.tar.xz -C ${_font}
  done
}

package_ttf-0xproto-nerd() {
  pkgdesc="Patched font 0xProto from nerd fonts library"
  license=('OFL-1.1-no-RFN')

  install -Dm644 0xproto/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 0xproto/LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_ttf-3270-nerd() {
  pkgdesc="Patched font IBM 3270 from nerd fonts library"
  license=('BSD-3-Clause')

  install -Dm644 3270/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 3270/LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_ttf-agave-nerd() {
  pkgdesc="Patched font Agave from nerd fonts library"
  license=('MIT')
  conflicts=(nerd-fonts-agave)
  replaces=(nerd-fonts-agave)

  install -Dm644 agave/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 agave/LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_ttf-anonymouspro-nerd() {
  pkgdesc="Patched font Anonymous Pro (Anonymice) from nerd fonts library"
  license=('OFL-1.1-RFN')
  conflicts=(nerd-fonts-anonymous-pro)
  replaces=(nerd-fonts-anonymous-pro)

  install -Dm644 anonymouspro/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 anonymouspro/OFL.txt -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_ttf-arimo-nerd() {
  pkgdesc="Patched font Arimo from nerd fonts library"
  license=('Apache-2.0')
  conflicts=(nerd-fonts-arimo)
  replaces=(nerd-fonts-arimo)

  install -Dm644 arimo/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 arimo/LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_otf-aurulent-nerd() {
  pkgdesc="Patched font Aurulent Sans Mono from nerd fonts library"
  license=('OFL-1.1-no-RFN')

  install -Dm644 aurulent/*.otf -t "${pkgdir}/usr/share/fonts/OTF"
  install -Dm644 aurulent/"SIL Open Font License.txt" \
          -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_ttf-bigblueterminal-nerd() {
  pkgdesc="Patched font Big Blue Terminal from nerd fonts library"
  license=('CC-BY-SA-4.0')

  install -Dm644 bigblueterminal/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 bigblueterminal/LICENSE.TXT \
          -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_ttf-bitstream-vera-mono-nerd() {
  pkgdesc="Patched font Bitstream Vera Mono from nerd fonts library"
  license=('Bitstream-Vera')
  conflicts=(nerd-fonts-bitstream-vera-mono)
  replaces=(nerd-fonts-bitstream-vera-mono)

  install -Dm644 bitstreamveramono/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 bitstreamveramono/"Bitstream Vera License.txt" \
          -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_ttf-cascadia-code-nerd() {
  pkgdesc="Patched font Cascadia Code (Caskaydia) from nerd fonts library"
  license=('OFL-1.1-RFN')
  conflicts=(nerd-fonts-cascadia-code)
  replaces=(nerd-fonts-cascadia-code otf-cascadia-code-nerd)

  install -Dm644 cascadiacode/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 cascadiacode/LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_ttf-cascadia-mono-nerd() {
  pkgdesc="Patched font Cascadia Mono (Caskaydia) from nerd fonts library"
  license=('OFL-1.1-RFN')

  install -Dm644 cascadiamono/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 cascadiamono/LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_otf-codenewroman-nerd() {
  pkgdesc="Patched font Code New Roman from nerd fonts library"
  license=('OFL-1.1-no-RFN')

  install -Dm644 codenewroman/*.otf -t "${pkgdir}/usr/share/fonts/OTF"
  install -Dm644 codenewroman/license.txt -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_otf-comicshanns-nerd() {
  pkgdesc="Patched font Comic Sans (Comic Shanns) from nerd fonts library"
  license=('MIT')

  install -Dm644 comicshanns/*.otf -t "${pkgdir}/usr/share/fonts/OTF"
  install -Dm644 comicshanns/LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_otf-commit-mono-nerd() {
  pkgdesc="Patched font Commit Mono from nerd fonts library"
  license=('OFL-1.1-no-RFN')

  install -Dm644 commitmono/*.otf -t "${pkgdir}/usr/share/fonts/OTF"
  install -Dm644 commitmono/LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_ttf-cousine-nerd() {
  pkgdesc="Patched font Cousine from nerd fonts library"
  license=('Apache-2.0')

  install -Dm644 cousine/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 cousine/LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_ttf-d2coding-nerd() {
  pkgdesc="Patched font D2Coding from nerd fonts library"
  license=('OFL-1.1-no-RFN')

  install -Dm644 d2coding/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 d2coding/LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_ttf-daddytime-mono-nerd() {
  pkgdesc="Patched font Daddy Time Mono from nerd fonts library"
  license=('OFL-1.1-no-RFN')

  install -Dm644 daddytimemono/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 daddytimemono/LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_ttf-dejavu-nerd() {
  pkgdesc="Patched font Dejavu Sans Mono from nerd fonts library"
  license=('Bitstream-Vera')
  conflicts=(nerd-fonts-dejavu-complete)
  replaces=(nerd-fonts-dejavu-complete)

  install -Dm644 dejavu/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 dejavu/LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_otf-droid-nerd() {
  pkgdesc="Patched font Droid Sans Mono from nerd fonts library"
  license=('Apache-2.0')
  conflicts=(nerd-fonts-droid-sans-mono)
  replaces=(nerd-fonts-droid-sans-mono)

  install -Dm644 droid/*.otf -t "${pkgdir}/usr/share/fonts/OTF"
  install -Dm644 droid/LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_ttf-envycoder-nerd() {
  pkgdesc="Patched font Envy Code R from nerd fonts library"
  license=('OFL-1.1-RFN')

  install -Dm644 envycoder/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 envycoder/LICENCE.md -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_ttf-fantasque-nerd() {
  pkgdesc="Patched font Fantasque Sans Mono from nerd fonts library"
  license=('OFL-1.1-no-RFN')
  conflicts=(nerd-fonts-fantasque-sans-mono)
  replaces=(nerd-fonts-fantasque-sans-mono)

  install -Dm644 fantasque/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 fantasque/OFL.txt -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_ttf-firacode-nerd() {
  pkgdesc="Patched font Fira (Fura) Code from nerd fonts library"
  license=('OFL-1.1-no-RFN')
  conflicts=(nerd-fonts-fira-code)
  replaces=(nerd-fonts-fira-code)

  install -Dm644 fira/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 fira/LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_otf-firamono-nerd() {
  pkgdesc="Patched font Fira (Fura) Mono from nerd fonts library"
  license=('OFL-1.1-no-RFN')
  conflicts=(nerd-fonts-fira-mono)
  replaces=(nerd-fonts-fira-mono)

  install -Dm644 firamono/*.otf -t "${pkgdir}/usr/share/fonts/OTF"
  install -Dm644 firamono/LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_otf-geist-mono-nerd() {
  pkgdesc="Patched font Geist Mono from nerd fonts library"
  license=('OFL-1.1-no-RFN')

  install -Dm644 geistmono/*.otf -t "${pkgdir}/usr/share/fonts/OTF"
  install -Dm644 geistmono/LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_ttf-go-nerd() {
  pkgdesc="Patched font Go Mono from nerd fonts library"
  license=('BSD-3-Clause-Clear')
  conflicts=(nerd-fonts-go-mono)
  replaces=(nerd-fonts-go-mono)

  install -Dm644 go/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 go/LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_ttf-gohu-nerd() {
  pkgdesc="Patched font Gohufont from nerd fonts library"
  license=('WTFPL')

  install -Dm644 gohu/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 gohu/COPYING-LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_ttf-hack-nerd() {
  pkgdesc="Patched font Hack from nerd fonts library"
  license=('MIT' 'Bitstream-Vera')
  conflicts=(nerd-fonts-hack)
  replaces=(nerd-fonts-hack)

  install -Dm644 hack/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 hack/LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_otf-hasklig-nerd() {
  pkgdesc="Patched font Hasklig (Hasklug) from nerd fonts library"
  license=('OFL-1.1-RFN')
  conflicts=(nerd-fonts-hasklig)
  replaces=(nerd-fonts-hasklig)

  install -Dm644 hasklig/*.otf -t "${pkgdir}/usr/share/fonts/OTF"
  install -Dm644 hasklig/LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_ttf-heavydata-nerd() {
  pkgdesc="Patched font Heavy Data from nerd fonts library"
  license=('LicenseRef-Vic-Fieger-License')

  install -Dm644 heavydata/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 heavydata/"Vic Fieger License.txt" \
          -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_otf-hermit-nerd() {
  pkgdesc="Patched font Hermit from nerd fonts library"
  license=('OFL-1.1-RFN')
  conflicts=(nerd-fonts-hermit)
  replaces=(nerd-fonts-hermit)

  install -Dm644 hermit/*.otf -t "${pkgdir}/usr/share/fonts/OTF"
  install -Dm644 hermit/LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_ttf-iawriter-nerd() {
  pkgdesc="Patched font iA Writer (iM Writing) from nerd fonts library"
  license=('OFL-1.1-RFN')

  install -Dm644 iawriter/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 iawriter/LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_ttf-ibmplex-mono-nerd() {
  pkgdesc="Patched font IBM Plex Mono (Blex) from nerd fonts library"
  license=('OFL-1.1-RFN')
  conflicts=(nerd-fonts-ibm-plex-mono)
  replaces=(nerd-fonts-ibm-plex-mono)

  install -Dm644 ibmplexmono/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 ibmplexmono/LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_ttf-inconsolata-nerd() {
  pkgdesc="Patched font Inconsolata from nerd fonts library"
  license=('OFL-1.1-no-RFN')
  conflicts=(nerd-fonts-inconsolata)
  replaces=(nerd-fonts-inconsolata)

  install -Dm644 inconsolata/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 inconsolata/OFL.txt -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_ttf-inconsolata-go-nerd() {
  pkgdesc="Patched font Inconsolata Go from nerd fonts library"
  license=('OFL-1.1-no-RFN')
  conflicts=(nerd-fonts-inconsolata-go)
  replaces=(nerd-fonts-inconsolata-go)

  install -Dm644 inconsolatago/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 inconsolatago/OFL.txt -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_ttf-inconsolata-lgc-nerd() {
  pkgdesc="Patched font Inconsolata LGC from nerd fonts library"
  license=('OFL-1.1-no-RFN')
  conflicts=(nerd-fonts-inconsolatalgc)
  replaces=(nerd-fonts-inconsolatalgc)

  install -Dm644 inconsolatalgc/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 inconsolatalgc/LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_ttf-intone-nerd() {
  pkgdesc="Patched font Intone (Intel One) from nerd fonts library"
  license=('OFL-1.1-RFN')

  install -Dm644 intone/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 intone/LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_ttf-iosevka-nerd() {
  pkgdesc="Patched font Iosevka from nerd fonts library"
  license=('OFL-1.1-no-RFN')
  conflicts=(nerd-fonts-iosevka)
  replaces=(nerd-fonts-iosevka)

  install -Dm644 iosevka/*.ttf -t "${pkgdir}"/usr/share/fonts/TTF/
  install -Dm644 iosevka/LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_ttf-iosevkaterm-nerd() {
  pkgdesc="Patched font Iosevka Term from nerd fonts library"
  license=('OFL-1.1-no-RFN')

  install -Dm644 iosevkaterm/*.ttf -t "${pkgdir}"/usr/share/fonts/TTF/
  install -Dm644 iosevkaterm/LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_ttf-iosevkatermslab-nerd() {
  pkgdesc="Patched font Iosevka Term Slab from nerd fonts library"
  license=('OFL-1.1-no-RFN')

  install -Dm644 iosevkatermslab/*.ttf -t "${pkgdir}"/usr/share/fonts/TTF/
  install -Dm644 iosevkatermslab/LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_ttf-jetbrains-mono-nerd() {
  pkgdesc="Patched font JetBrains Mono from nerd fonts library"
  license=('OFL-1.1-no-RFN')
  conflicts=(nerd-fonts-jetbrains-mono)
  replaces=(nerd-fonts-jetbrains-mono)

  install -Dm644 jetbrainsmono/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 jetbrainsmono/OFL.txt -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_ttf-lekton-nerd() {
  pkgdesc="Patched font Lekton from nerd fonts library"
  license=('OFL-1.1-no-RFN')

  install -Dm644 lekton/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 lekton/"SIL Open Font License.txt" \
          -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_ttf-liberation-mono-nerd() {
  pkgdesc="Patched font Liberation Mono from nerd fonts library"
  license=('OFL-1.1-RFN')
  conflicts=(nerd-fonts-liberation-mono)
  replaces=(nerd-fonts-liberation-mono)

  install -Dm644 liberationmono/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 liberationmono/LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_ttf-lilex-nerd() {
  pkgdesc="Patched font Lilex from nerd fonts library"
  license=('OFL-1.1-no-RFN')

  install -Dm644 lilex/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 lilex/LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_ttf-martian-mono-nerd() {
  pkgdesc="Patched font Martian Mono from nerd fonts library"
  license=('OFL-1.1-no-RFN')

  install -Dm644 martianmono/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 martianmono/LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_ttf-meslo-nerd() {
  pkgdesc="Patched font Meslo LG from nerd fonts library"
  license=('Apache-2.0')
  conflicts=(nerd-fonts-meslo)
  replaces=(nerd-fonts-meslo)

  install -Dm644 meslo/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 meslo/LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_otf-monaspace-nerd() {
  pkgdesc="Patched font Monaspace (Monaspice) from nerd fonts library"
  license=('OFL-1.1-RFN')

  install -Dm644 monaspace/*.otf -t "${pkgdir}/usr/share/fonts/OTF"
  install -Dm644 monaspace/LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_ttf-monofur-nerd() {
  pkgdesc="Patched font Monofur from nerd fonts library"
  license=('LicenseRef-Monofur-Free-License')
  conflicts=(nerd-fonts-monofur)
  replaces=(nerd-fonts-monofur)

  install -Dm644 monofur/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 monofur/LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_ttf-monoid-nerd() {
  pkgdesc="Patched font Monoid from nerd fonts library"
  license=('MIT' 'OFL-1.1-no-RFN')
  conflicts=(nerd-fonts-monoid)
  replaces=(nerd-fonts-monoid)

  install -Dm644 monoid/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 monoid/LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_ttf-mononoki-nerd() {
  pkgdesc="Patched font Mononoki from nerd fonts library"
  license=('OFL-1.1-RFN')
  conflicts=(nerd-fonts-mononoki)
  replaces=(nerd-fonts-mononoki)

  install -Dm644 mononoki/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 mononoki/LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_ttf-mplus-nerd() {
  pkgdesc="Patched font MPlus from nerd fonts library"
  license=('OFL-1.1-no-RFN')
  conflicts=(nerd-fonts-mplus)
  replaces=(nerd-fonts-mplus)

  install -Dm644 mplus/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 mplus/OFL.txt -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_ttf-noto-nerd() {
  pkgdesc="Patched font Noto from nerd fonts library"
  license=('OFL-1.1-no-RFN')
  conflicts=(nerd-fonts-noto)
  replaces=(nerd-fonts-noto)

  install -Dm644 noto/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 noto/LICENSE_OFL.txt -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_otf-opendyslexic-nerd() {
  pkgdesc="Patched font OpenDyslexic from nerd fonts library"
  license=('Bitstream-Vera')

  install -Dm644 opendyslexic/*.otf -t "${pkgdir}/usr/share/fonts/OTF"
  install -Dm644 opendyslexic/LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_otf-overpass-nerd() {
  pkgdesc="Patched font Overpass from nerd fonts library"
  license=('OFL-1.1-no-RFN' 'LGPL-2.1-only')
  conflicts=(nerd-fonts-overpass)
  replaces=(nerd-fonts-overpass)

  install -Dm644 overpass/*.otf -t "${pkgdir}/usr/share/fonts/OTF"
  install -Dm644 overpass/LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_ttf-profont-nerd() {
  pkgdesc="Patched font ProFont from nerd fonts library"
  license=('MIT')
  conflicts=(nerd-fonts-profont)
  replaces=(nerd-fonts-profont)

  install -Dm644 profont/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 profont/LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_ttf-proggyclean-nerd() {
  pkgdesc="Patched font ProggyClean from nerd fonts library"
  license=('MIT')

  install -Dm644 proggyclean/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 proggyclean/Licence.txt -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_ttf-recursive-nerd() {
  pkgdesc="Patched font Recursive Mono (Rec Mono) from nerd fonts library"
  license=('OFL-1.1-no-RFN')


  install -Dm644 recursive/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 recursive/LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_ttf-roboto-mono-nerd() {
  pkgdesc="Patched font Roboto Mono from nerd fonts library"
  license=('Apache-2.0')
  conflicts=(nerd-fonts-roboto-mono)
  replaces=(nerd-fonts-roboto-mono)

  install -Dm644 robotomono/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 robotomono/LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_ttf-sharetech-mono-nerd() {
  pkgdesc="Patched font Share Tech Mono (Shure Tech Mono) from nerd fonts library"
  license=('OFL-1.1-RFN')

  install -Dm644 sharetechmono/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 sharetechmono/OFL.txt -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_ttf-sourcecodepro-nerd() {
  pkgdesc="Patched font Source Code Pro from nerd fonts library"
  license=('OFL-1.1-RFN')
  conflicts=(nerd-fonts-source-code-pro)
  replaces=(nerd-fonts-source-code-pro)

  install -Dm644 sourcecodepro/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 sourcecodepro/LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_ttf-space-mono-nerd() {
  pkgdesc="Patched font Space Mono from nerd fonts library"
  license=('OFL-1.1-no-RFN')
  conflicts=(nerd-fonts-source-space-mono)
  replaces=(nerd-fonts-source-space-mono)

  install -Dm644 spacemono/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 spacemono/OFL.txt -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_ttf-terminus-nerd() {
  pkgdesc="Patched font Terminus (Terminess) from nerd fonts library"
  license=('OFL-1.1-RFN')
  conflicts=(nerd-fonts-terminus)
  replaces=(nerd-fonts-terminus)

  install -Dm644 terminus/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 terminus/LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_ttf-tinos-nerd() {
  pkgdesc="Patched font Tinos from nerd fonts library"
  license=('Apache-2.0')
  conflicts=(nerd-fonts-source-tinos)
  replaces=(nerd-fonts-source-tinos)

  install -Dm644 tinos/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 tinos/"Apache License.txt" -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_ttf-ubuntu-mono-nerd() {
  pkgdesc="Patched font Ubuntu Mono from nerd fonts library"
  license=('LicenseRef-Ubuntu-Font-License-1.0')
  conflicts=(nerd-fonts-ubuntu-mono ttf-ubuntumono-nerd)
  replaces=(nerd-fonts-ubuntu-mono ttf-ubuntumono-nerd)

  install -Dm644 ubuntumono/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 ubuntumono/LICENCE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_ttf-ubuntu-nerd() {
  pkgdesc="Patched font Ubuntu from nerd fonts library"
  license=('LicenseRef-Ubuntu-Font-License-1.0')
  conflicts=(nerd-fonts-ubuntu)
  replaces=(nerd-fonts-ubuntu)

  install -Dm644 ubuntu/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 ubuntu/LICENCE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_ttf-victor-mono-nerd() {
  pkgdesc="Patched font Victor Mono from nerd fonts library"
  license=('OFL-1.1-no-RFN')
  conflicts=(ttf-nerd-fonts-victor-mono)
  replaces=(ttf-nerd-fonts-victor-mono)

  install -Dm644 victormono/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 victormono/LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}


package_ttf-zed-mono-nerd() {
  pkgdesc="Patched font Zed Mono from nerd fonts library"
  license=('OFL-1.1-no-RFN')

  install -Dm644 zedmono/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 zedmono/LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
